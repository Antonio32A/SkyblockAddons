buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'io.franzbecker:gradle-lombok:3.3.0'
    }
}

apply plugin: 'io.franzbecker.gradle-lombok'
apply plugin: 'net.minecraftforge.gradle.forge'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    mappings = "stable_22"

    // Choose the correct versions file for the update checker
    def updateJSON = (project.version.toString().contains('b')) ? betaJSON : releaseJSON

    // Replace placeholders in source code
    replace("{version}": project.version, "{updateJSON}": updateJSON)
    replaceIn("SkyblockAddons.java")
}

jar {
    archiveName = archivesBaseName + "-" + version + "-for-MC-1.8.9.jar"

    manifest.attributes(
            'Main-Class': 'OpenErrorMessage',
            'FMLCorePlugin': 'codes.biscuit.skyblockaddons.tweaker.SkyblockAddonsLoadingPlugin',
            "ForceLoadAsMod": true,
            'FMLCorePluginContainsFMLMod': true,
            'ModSide': 'CLIENT',
            'FMLAT': 'skyblockaddons_at.cfg'
    )

}

configurations {
    provided
    compile.extendsFrom(provided)
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'litemod.json'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'litemod.json'
    }
}

/*
 If you're using IntelliJ IDEA, make sure to run this task before running Minecraft in your dev environment
 or the mod assets won't load.
 */
// Copies mod resources to the development environment
task processResourcesDev(type: Copy) {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'litemod.json'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'litemod.json'
    }
    into(sourceSets.main.java.outputDir)
}

//reobf { // Only for labymod client.
//    jar {
//        useNotchSrg()
//    }
//}